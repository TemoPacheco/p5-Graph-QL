#!/usr/bin/env plackup

# PODNAME: bin/graphiql
# ABSTRACT: A demo of Graph::QL::Web using the graphiql client

use v5.24;
use warnings;
use experimental 'signatures';

use FindBin;
use lib "$FindBin::Bin/../lib";

use Plack;
use Plack::Builder;
use Plack::App::File;

use Graph::QL::Web;

use Graph::QL::Schema;
use Graph::QL::Resolver::SchemaResolver;
use Graph::QL::Resolver::TypeResolver;
use Graph::QL::Resolver::FieldResolver;

my $schema = Graph::QL::Schema->new_from_source(q[
    scalar Boolean
    scalar String

    type Query {
        hello : String
    }

    schema { query : Query }
]);

my $resolvers = Graph::QL::Resolver::SchemaResolver->new(
    types => [
        Graph::QL::Resolver::TypeResolver->new(
            name   => 'Query',
            fields => [
                Graph::QL::Resolver::FieldResolver->new( name => 'hello', code => sub ($, $, $, $) { 'Hello World' } )
            ]
        )
    ]
);

builder {

    mount '/css/graphiql.css'   => Plack::App::File->new( file => $FindBin::Bin.'/../root/static/css/graphiql.css'   )->to_app;
    mount '/js/graphiql.min.js' => Plack::App::File->new( file => $FindBin::Bin.'/../root/static/js/graphiql.min.js' )->to_app;
    mount '/index.html'         => Plack::App::File->new( file => $FindBin::Bin.'/../root/static/index.html'         )->to_app;
    mount '/graphql'            => Graph::QL::Web->new(
        schema    => $schema,
        resolvers => $resolvers,
    )->to_app;

    mount '/' => sub { [ 302, [ Location => '/index.html' ], []] };
};



